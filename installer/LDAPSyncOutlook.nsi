; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "LDAPSyncOutlook"
!define PRODUCT_VERSION "3.2"
!define PRODUCT_PUBLISHER "SDG"
!define PRODUCT_WEB_SITE "http://www.sdg.com.co"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!define ALL_USERS
!include "MUI.nsh"
!include "path.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "licencia.txt"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Spanish"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "LDAPSyncOutlookInstaller-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\SDG"
ShowInstDetails hide
ShowUnInstDetails hide

Function .onInit
        ClearErrors
	UserInfo::GetName
	IfErrors done
	Pop $0
	UserInfo::GetAccountType
	Pop $1
        StrCmp $1 "Admin" 0 +2
		Goto done
        StrCmp $1 "Power" 0 +2
               Goto done
        MessageBox MB_ICONSTOP 'No tiene suficientes privilegios para instalar ${PRODUCT_NAME}'
        abort
	done:
FunctionEnd

Function GetWindowsVersion

   Push $R0
   Push $R1

   ClearErrors

   ReadRegStr $R0 HKLM \
   "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion

   IfErrors 0 lbl_winnt

   ; we are not NT
   ReadRegStr $R0 HKLM \
   "SOFTWARE\Microsoft\Windows\CurrentVersion" VersionNumber

   StrCpy $R1 $R0 1
   StrCmp $R1 '4' 0 lbl_error

   StrCpy $R1 $R0 3

   StrCmp $R1 '4.0' lbl_win32_95
   StrCmp $R1 '4.9' lbl_win32_ME lbl_win32_98

   lbl_win32_95:
     StrCpy $R0 '95'
   Goto lbl_done

   lbl_win32_98:
     StrCpy $R0 '98'
   Goto lbl_done

   lbl_win32_ME:
     StrCpy $R0 'ME'
   Goto lbl_done

   lbl_winnt:

   StrCpy $R1 $R0 1

   StrCmp $R1 '3' lbl_winnt_x
   StrCmp $R1 '4' lbl_winnt_x

   StrCpy $R1 $R0 3

   StrCmp $R1 '5.0' lbl_winnt_2000
   StrCmp $R1 '5.1' lbl_winnt_XP
   StrCmp $R1 '5.2' lbl_winnt_2003 lbl_error

   lbl_winnt_x:
     StrCpy $R0 "NT $R0" 6
   Goto lbl_done

   lbl_winnt_2000:
     Strcpy $R0 '2000'
   Goto lbl_done

   lbl_winnt_XP:
     Strcpy $R0 'XP'
   Goto lbl_done

   lbl_winnt_2003:
     Strcpy $R0 '2003'
   Goto lbl_done

   lbl_error:
     Strcpy $R0 ''
   lbl_done:

   Pop $R1
   Exch $R0

FunctionEnd

Function InstallDSClient

         Call GetWindowsVersion
;       Windows Version (95, 98, ME, NT x.x, 2000, XP, 2003)
         Pop $R0
         strcmp $R0 '95' lbl_install
         strcmp $R0 '98' lbl_install
         strcmp $R0 'ME' lbl_install
         goto lbl_done

lbl_install:
        ReadRegStr $R0 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DsClient" DisplayName
        StrCmp $R0 "" lbl_dsclient
        Goto lbl_done
lbl_dsclient:
     SetOutPath "$TEMP"
     SetOverwrite on
     File "temp\DSCLIENT.EXE"
     Exec "$TEMP\DSCLIENT.EXE"
     Delete /REBOOTOK "$TEMP\DSCLIENT.EXE"
lbl_done:
FunctionEnd



Section "-hidden Común" SEC03
  ExecWait  "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe -close"
  Sleep     1000

  SetOutPath $TEMP
  File "vcredist\vcredist_x86.exe"
  ExecWait '$TEMP\vcredist_x86.exe /q'
  Delete "$TEMP\vcredist_x86.exe"

  SetOutPath "$INSTDIR\LDAPSyncOutlook"
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\SDG"
  CreateDirectory "$SMPROGRAMS\SDG\LDAPSyncOutlook"

  WriteRegStr HKLM "Software\SDG\LDAPSyncOutlook" "InstallPath" "$INSTDIR\LDAPSyncOutlook\"
  
  SetOutPath "$INSTDIR\LDAPSyncOutlook"
SectionEnd

Section "Outlook Express" SEC01

  SetOutPath "$INSTDIR\LDAPSyncOutlook"
  SetOverwrite on
  File "SDG\LDAPSyncOutlook\LDAPSyncOutlook.exe"

  SetOutPath "$INSTDIR\LDAPSyncOutlook"
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\SDG"
  CreateDirectory "$SMPROGRAMS\SDG\LDAPSyncOutlook"
  CreateShortCut "$SMPROGRAMS\SDG\LDAPSyncOutlook\LDAPSyncOutlook.lnk" "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe"

  CreateShortCut "$DESKTOP\LDAPSyncOutlook.lnk" "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe"

; agregamos al inicio la aplicacion
  WriteRegStr HKLM "Software\Microsoft\windows\CurrentVersion\Run" "LDAPSyncOutlook" "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe -start"
SectionEnd

Section "Microsoft Outlook" SEC02

  Push "$INSTDIR\LDAPSyncOutlook"
  Call AddToPath

  Push "PATH"
  Push "$INSTDIR\LDAPSyncOutlook"
  Call AddToEnvVar
  ; registramos el dll de outlook.
  SetOutPath "$INSTDIR\LDAPSyncOutlook"
  File "SDG\LDAPSyncOutlook\LDAPSyncOutlookAddin.dll"
  RegDLL "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlookAddin.dll"
  ; agregamos al inicio para que cada vez que se reinicie se autocoloque
  WriteRegStr HKLM "Software\Microsoft\windows\CurrentVersion\Run" "LDAPSyncOutlook.Addin" '$SYSDIR\regsvr32.exe /s "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlookAddin.dll"'
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\LDAPSyncOutlookU.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\LDAPSyncOutlookU.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
   
  call InstallDSClient
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC03} "Librerias comunes"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Compatibilidad con Microsoft Outlook"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Compatibilidad con Outlook Express"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "La desinstalación de $(^Name) finalizó satisfactoriamente."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "¿Está completamente seguro que desea desinstalar $(^Name) junto con todos sus componentes?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  ExecWait  "$INSTDIR\LDAPSyncOutlook\LDAPSyncOutlook.exe -close"
  
  Delete "$INSTDIR\LDAPSyncOutlookU.exe"
  RMDir "$INSTDIR"

  RMDir /r "$INSTDIR\LDAPSyncOutlook"
  
  SetShellVarContext all
  RMDir /r "$SMPROGRAMS\SDG\LDAPSyncOutlook"
  RMDir "$SMPROGRAMS\SDG"
  
  Delete "$DESKTOP\LDAPSyncOutlook.lnk"
  
  ; lo eliminamos del inicio.
  DeleteRegValue HKLM "Software\Microsoft\windows\CurrentVersion\Run" "LDAPSyncOutlook"
  
  DeleteRegKey HKLM "Software\SDG\LDAPSyncOutlook"
  DeleteRegKey HKCU "Software\SDG\LDAPSyncOutlook"
  
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd

